;; nbb smoke test — proves the :cljs branch is real.
;;
;; sd_hash is a SHA-256 over the US-ASCII bytes of the presentation prefix
;; (RFC 9901 §4.3.1). If the hosts encode those bytes differently, a Holder on one
;; produces a proof a Verifier on the other rejects as :bad-sd-hash — which reads
;; as a tampering attempt and is not one.
;;
;;   npm install && npm run smoke
(ns nbb-smoke
  (:require [kotoba.lang.text :as str]
            [jws.core]
            [sd-jwt.core :as sd]
            [sd-jwt-vc.core :as vc]))

(def ^:private failures (atom 0))
(defn- check [label expected actual]
  (if (= expected actual)
    (println "  ok  " label)
    (do (swap! failures inc)
        (println "  FAIL" label "\n        expected:" (pr-str expected)
                 "\n        actual:  " (pr-str actual)))))
(defn- threw [f] (try (f) :no-throw (catch :default _ :threw)))

(def codec {:json-encode (fn [v] (js/JSON.stringify (clj->js v)))
            :json-decode (fn [s] (js->clj (js/JSON.parse s)))})
(defn- salts [] (let [n (atom 0)] (fn [] (str "salt000000000000000000" (swap! n inc)))))

;; A toy signer: the structure and the rules are what is under test here; the JVM
;; suite wires real Ed25519 for both issuer and holder.
(defn- toy [input] (sd/b64url (str "SIG:" (sd/b64url input))))
(defn- toy-bytes [input] (sd/b64url-decode-string (toy input)))
(defn- opts [& {:as extra}]
  (merge codec {:salt-fn (salts) :shuffle-fn identity :alg "EdDSA"
                :sign (fn [i] (js/Uint8Array.from
                               (into-array (map #(.charCodeAt (toy-bytes i) %)
                                                (range (count (toy-bytes i)))))))
                :verify (fn [_ _] true)}
         extra))

(def claims
  {"iss" "https://acme.example"
   "vct" "https://acme.example/credentials/membership"
   "exp" 1893456000
   "role" "auditor"
   "name" "Alice"})

(println "sd-jwt-vc :cljs smoke")

;; The cross-host invariant: sd_hash over a fixed presentation prefix. Pinned
;; identically in test/sd_jwt_vc/core_test.clj.
(check "sd_hash over a fixed prefix"
       "_00mjj_YhwRTfK_jKgm8mqjuxlcnaupJJ_Ehm-A4Au4"
       (vc/sd-hash "JWT~d1~d2~"))

(check "the prefix keeps its trailing tilde" "JWT~d1~d2~"
       (vc/presentation-prefix "JWT" ["d1" "d2"]))
(check "a prefix without it is refused" :threw
       (threw #(vc/sd-hash "JWT~d1~d2")))
(check "sd_hash changes with the disclosure set" false
       (= (vc/sd-hash "JWT~d1~d2~") (vc/sd-hash "JWT~d1~")))

;; the seven protected claims
(check "the protected set is exactly the draft's seven"
       #{"iss" "nbf" "exp" "cnf" "vct" "vct#integrity" "status"}
       vc/protected-claims)
(doseq [c ["iss" "exp" "vct" "status"]]
  (check (str "concealing " c " is refused") :threw
         (threw #(vc/issue claims [[c]] (opts)))))

;; required claims
(check "vct is required" :threw (threw #(vc/issue (dissoc claims "vct") [] (opts))))
(check "iss is required" :threw (threw #(vc/issue (dissoc claims "iss") [] (opts))))

;; issuance shape
(let [{:keys [issuer-jwt disclosures presentation]}
      (vc/issue claims [["name"]] (opts))]
  (check "one disclosure" 1 (count disclosures))
  (check "typ is dc+sd-jwt" "dc+sd-jwt"
         (get (jws.core/decode-header issuer-jwt codec) "typ"))
  (check "presentation ends with a tilde" true
         (str/ends-with? presentation "~")))

(check "typ constants" ["dc+sd-jwt" "vc+sd-jwt" "kb+jwt"]
       [vc/sd-jwt-vc-typ vc/legacy-typ vc/kb-jwt-typ])

(println (if (zero? @failures)
           "all sd-jwt-vc :cljs checks passed"
           (str @failures " sd-jwt-vc :cljs check(s) FAILED")))
(when (pos? @failures) (throw (js/Error. (str @failures " failure(s)"))))
